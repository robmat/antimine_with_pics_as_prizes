plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

android {
    namespace 'dev.lucasnlm.antimine.gdx'

    defaultConfig {
        // versionCode and versionName must be hardcoded to support F-droid
        minSdk 23
        targetSdk 36
        compileSdk = 36
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlin {
        jvmToolchain(17)
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

configurations { natives }

dependencies {
    implementation project(':core')
    implementation project(':preferences')
    implementation project(':ui')

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.activity:activity-ktx:1.11.0'
    implementation 'androidx.fragment:fragment-ktx:1.8.9'

    // Koin
    implementation 'io.insert-koin:koin-android:4.1.1'
    testImplementation 'io.insert-koin:koin-test:4.1.1'

    // LibGDX
    api 'com.badlogicgames.gdx:gdx-backend-android:1.13.1'
    api 'com.badlogicgames.gdx:gdx:1.13.1'
    natives 'com.badlogicgames.gdx:gdx-platform:1.13.1:natives-armeabi-v7a'
    natives 'com.badlogicgames.gdx:gdx-platform:1.13.1:natives-arm64-v8a'
    natives 'com.badlogicgames.gdx:gdx-platform:1.13.1:natives-x86'
    natives 'com.badlogicgames.gdx:gdx-platform:1.13.1:natives-x86_64'
}

tasks.register('copyAndroidNatives') {
    doFirst {
        file("libs/armeabi-v7a/").mkdirs()
        file("libs/arm64-v8a/").mkdirs()
        file("libs/x86_64/").mkdirs()
        file("libs/x86/").mkdirs()

        configurations.natives.files.each { jar ->
            def outputDir = null
            if (jar.name.endsWith("natives-arm64-v8a.jar")) outputDir = file("libs/arm64-v8a")
            if (jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("libs/armeabi-v7a")
            if (jar.name.endsWith("natives-x86_64.jar")) outputDir = file("libs/x86_64")
            if (jar.name.endsWith("natives-x86.jar")) outputDir = file("libs/x86")
            if (outputDir != null) {
                copy {
                    from zipTree(jar)
                    into outputDir
                    include "*.so"
                }
            }
        }
    }
}

preBuild.dependsOn copyAndroidNatives

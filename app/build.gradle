apply plugin: 'com.android.application'

if (System.getenv('IS_RELEASE_BUILD')) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
    apply plugin: 'com.google.firebase.firebase-perf'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    namespace 'dev.lucasnlm.antimine'

    defaultConfig {
        // versionCode and versionName must be hardcoded to support F-droid
        versionCode 1703001
        versionName '17.3.0'
        minSdk 21
        targetSdk 34
        compileSdk 34
        multiDexEnabled true
        vectorDrawables.useSupportLibrary true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    signingConfigs {
        release {
            if (System.getenv('IS_RELEASE_BUILD')) {
                storeFile file(System.getenv('KEYSTORE'))
                keyAlias System.getenv('KEY_ALIAS')
                storePassword System.getenv('KEY_STORE_PASSWORD')
                keyPassword System.getenv('KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_17
        sourceCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    testOptions {
        unitTests {
            includeAndroidResources true
            animationsDisabled true
        }
    }

    flavorDimensions = ['version']
    productFlavors {
        google {
            dimension 'version'
            applicationId 'dev.lucasnlm.antimine'
            versionNameSuffix " S"
        }

        googleInstant {
            versionCode 157
            dimension 'version'
            applicationId 'dev.lucasnlm.antimine'
            versionNameSuffix " I"
        }

        foss {
            dimension 'version'
            // There's a typo on F-Droid release :(
            applicationId 'dev.lucanlm.antimine'
            versionNameSuffix " F"
        }
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    // Dependencies must be hardcoded to support F-droid

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':external')
    implementation project(':common')
    implementation project(':control')
    implementation project(':about')
    implementation project(':ui')
    implementation project(':preferences')
    implementation project(':themes')
    implementation project(':tutorial')
    implementation project(':core')
    implementation project(':gdx')

    googleImplementation project(':proprietary')
    googleInstantImplementation project(':proprietary')
    googleInstantImplementation project(':instant')
    fossImplementation project(':foss')
    fossImplementation project(':donation')

    googleImplementation project(':audio')
    fossImplementation project(':audio')
    googleInstantImplementation project(':audio-low')

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.activity:activity-ktx:1.7.2'
    implementation 'androidx.fragment:fragment-ktx:1.6.1'

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.6.1'

    // Room
    implementation 'androidx.room:room-ktx:2.5.2'

    // Constraint
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Google
    implementation 'com.google.android.material:material:1.9.0'

    // Koin
    implementation 'io.insert-koin:koin-android:3.1.2'
    testImplementation 'io.insert-koin:koin-test:3.1.2'

    // Kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.10'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'

    // Konfetti
    implementation 'nl.dionsegijn:konfetti-xml:2.0.2'

    // Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.core:core-ktx:1.10.1'
    testImplementation 'androidx.test:core-ktx:1.5.0'
    testImplementation 'androidx.test:rules:1.5.0'
    testImplementation 'androidx.test:runner:1.5.2'
    testImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    testImplementation 'androidx.fragment:fragment-testing:1.6.1'
    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation 'androidx.test.ext:junit:1.1.5'
    testImplementation 'io.mockk:mockk:1.13.5'

    // Core library
    androidTestImplementation 'androidx.test:core:1.5.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:core-ktx:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestUtil 'androidx.test:orchestrator:1.4.2'
}

tasks.withType(Test).configureEach {
    testLogging {
        exceptionFormat 'full'
    }
    afterTest { desc, result ->
        println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}
